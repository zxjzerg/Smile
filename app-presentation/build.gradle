apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    defaultConfig {
        applicationId "com.zxjdev.smile"
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName

        buildConfigField "String", "LEANCOULD_APP_KEY", rootProject.ext.leancloud_app_key
        buildConfigField "String", "LEANCOULD_APP_ID", rootProject.ext.leancloud_app_id
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        // 遍历所有的variant并执行重命名逻辑
        applicationVariants.all { variant ->
            if (variant.name.contains("release")) {
                renameAPK(variant)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "mode"

    productFlavors {
        develop {
            dimension "mode"
            minSdkVersion 21
        }
    }
}

androidExtensions {
    experimental = true
}

def renameAPK(variant) {
    variant.outputs.all { output ->

        // 获取HEAD的git分支名
        def branch = getGitRevParseInfo("--abbrev-ref")

        // 获取HEAD的commit信息
        def revision = ext.revision = getGitRevParseInfo("--short")

        // 重新生成一个输出的文件名
        def fileName = variant.buildType.name + "_" + branch + "_" + revision + ".apk"

        // 设置新的文件名给输出文件
        outputFileName = fileName
    }
}

// 获取git的信息
static def getGitRevParseInfo(what) {
    def cmd = "git rev-parse " + what + " HEAD"
    def process = cmd.execute()
    process.text.trim()
}

dependencies {
    implementation project(':app-domain')
    implementation project(':app-data')

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation rootProject.ext.android_support_v7
    implementation rootProject.ext.android_support_design

    implementation rootProject.ext.rxjava2
    implementation rootProject.ext.rxandroid2

    implementation rootProject.ext.dagger
    implementation rootProject.ext.dagger_android
    implementation rootProject.ext.dagger_android_support
    kapt rootProject.ext.dagger_processor
    kapt rootProject.ext.dagger_android_processor

    implementation rootProject.ext.leancloud_basic

    implementation rootProject.ext.timber

    implementation rootProject.ext.circle_image

    debugImplementation rootProject.ext.leak_canary_debug
    releaseImplementation leak_canary_release
    testImplementation leak_canary_release

    implementation rootProject.ext.stetho

    testImplementation rootProject.ext.junit

    implementation rootProject.ext.glide
    implementation rootProject.ext.glide_transformations

    implementation rootProject.ext.android_support_constaint

    implementation rootProject.ext.android_support_cardview

    implementation rootProject.ext.android_support_percent

    implementation rootProject.ext.image_crop
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.room:room-runtime:2.0.0'
    annotationProcessor 'androidx.room:room-compiler:2.0.0'

    debugImplementation 'com.zxjdev.smile.smile_flutter_module:flutter_debug:1.0'
    releaseImplementation 'com.zxjdev.smile.smile_flutter_module:flutter_release:1.0'
}

String storageUrl = System.env.FLUTTER_STORAGE_BASE_URL ?: "https://storage.googleapis.com"
repositories {
    maven {
        url '../smile_flutter_module/build/host/outputs/repo'
    }
    maven {
        url "$storageUrl/download.flutter.io"
    }
    mavenCentral()
}
